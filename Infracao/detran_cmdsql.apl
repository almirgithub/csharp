.head 0 +  Application Description: ---------------------------------------------------------------------------------------------------------------------
Detran-PE Departamento de Trânsito do estado de Pernambuco
Biblioteca de comandos de SQL
---------------------------------------------------------------------------------------------------------------------
Analista Responsável : 
---------------------------------------------------------------------------------------------------------------------
Migrado para versão CTD2000 em 19/08/2002 por Fernando Freitas
---------------------------------------------------------------------------------------------------------------------
.head 1 -  Outline Version - 4.0.28
.head 1 +  Design-time Settings
.data VIEWINFO
0000: 6F00000001000000 FFFF01000D004347 5458566965775374 6174650400010000
0020: 0000000000B40000 002C000000020000 0003000000FFFFFF FFFFFFFFFFF8FFFF
0040: FFE1FFFFFF2C0000 002C000000C40200 006C010000010000 0001000000010000
0060: 000F4170706C6963 6174696F6E497465 6D0100000012496E 7465726E616C2046
0080: 756E6374696F6E73
.enddata
.head 2 -  Outline Window State: Maximized
.head 2 +  Outline Window Location and Size
.data VIEWINFO
0000: 6600010003001B00 0200000000000000 0000AB20C2120500 1D00FFFF4D61696E
0020: 00
.enddata
.data VIEWSIZE
0000: 2100
.enddata
.head 3 -  Left: 5.088"
.head 3 -  Top: 0.01"
.head 3 -  Width:  5.088"
.head 3 -  Height: 5.865"
.head 2 +  Options Box Location
.data VIEWINFO
0000: D4186606B80B2A00
.enddata
.data VIEWSIZE
0000: 0800
.enddata
.head 3 -  Visible? Yes
.head 3 -  Left: 3.038"
.head 3 -  Top: 0.0"
.head 3 -  Width:  5.538"
.head 3 -  Height: 6.333"
.head 2 +  Class Editor Location
.head 3 -  Visible? No
.head 3 -  Left: 0.05"
.head 3 -  Top: 0.188"
.head 3 -  Width:  5.063"
.head 3 -  Height: 2.719"
.head 2 +  Tool Palette Location
.head 3 -  Visible? No
.head 3 -  Left: 7.3"
.head 3 -  Top: 0.885"
.head 2 -  Fully Qualified External References? No
.head 2 -  Reject Multiple Window Instances? Yes
.head 2 -  Enable Runtime Checks Of External References? Yes
.head 2 -  Use Release 4.0 Scope Rules? No
.head 1 +  Libraries
.head 2 -  File Include: sybsal32.apl
.head 2 -  File Include: dlgHelpMensagem.apl
.head 1 +  Global Declarations
.head 2 +  Window Defaults
.head 3 +  Tool Bar
.head 4 -  Display Style? Etched
.head 4 -  Font Name: System Default
.head 4 -  Font Size: System Default
.head 4 -  Font Enhancement: System Default
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Form Window
.head 4 -  Display Style? Etched
.head 4 -  Font Name: System Default
.head 4 -  Font Size: System Default
.head 4 -  Font Enhancement: System Default
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Dialog Box
.head 4 -  Display Style? Etched
.head 4 -  Font Name: System Default
.head 4 -  Font Size: System Default
.head 4 -  Font Enhancement: System Default
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Top Level Table Window
.head 4 -  Font Name: System Default
.head 4 -  Font Size: System Default
.head 4 -  Font Enhancement: System Default
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Data Field
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Multiline Field
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Spin Field
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Background Text
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Pushbutton
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 3 +  Radio Button
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Check Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Option Button
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 3 +  Group Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Child Table Window
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  List Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Combo Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Line
.head 4 -  Line Color: Use Parent
.head 3 +  Frame
.head 4 -  Border Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Picture
.head 4 -  Border Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 2 +  Formats
.head 3 -  Number: 0'%'
.head 3 -  Number: #0
.head 3 -  Number: ###000
.head 3 -  Number: ###000;'($'###000')'
.head 3 -  Date/Time: hh:mm:ss AMPM
.head 3 -  Date/Time: M/d/yy
.head 3 -  Date/Time: MM-dd-yy
.head 3 -  Date/Time: dd-MMM-yyyy
.head 3 -  Date/Time: MMM d, yyyy
.head 3 -  Date/Time: MMM d, yyyy hh:mm AMPM
.head 3 -  Date/Time: MMMM d, yyyy hh:mm AMPM
.head 3 -  Date/Time: dd-MM-yyyy
.head 3 -  Input: 99-99-9999
.head 3 -  Input: 9999.99
.head 3 -  Input: 9
.head 3 -  Input: 999999
.head 3 -  Input: #9
.head 3 -  Input: AAAAA
.head 2 +  External Functions
.head 2 +  Constants
.data CCDATA
0000: 3000000000000000 0000000000000000 00000000
.enddata
.data CCSIZE
0000: 1400
.enddata
.head 3 +  System
.head 3 -  User
.head 3 -  Enumerations
.head 2 +  Resources
.head 2 +  Variables
.head 3 -  Sql Handle: hSqlErro
.head 2 +  Internal Functions
.head 3 +  Function: fTratamentoErroSql
.head 4 -  Description: Trata as mensagens de erro de operações SQL.
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Number: wParam
.head 5 -  Number: lParam
.head 5 -  Sql Handle: hSql
.head 5 +  Boolean: bMostraNumero
.head 6 -  ! TRUE - Mostra número do Erro
.head 4 -  Static Variables
.head 4 +  Local variables
.head 5 -  Window Handle: hSqlErro
.head 5 -  Number: nErro
.head 5 -  Number: nPosErro
.head 5 -  String: sMsgErro
.head 5 -  Number: nSeveridade
.head 5 -  Number: nPosicaoBanco
.head 5 -  Number: nPosicaoTabela
.head 5 -  Number: nPosicaoConstraint
.head 5 -  String: sMensagem
.head 5 -  Number: nCodigoMSG
.head 5 -  Number: nCodigoSistema
.head 5 -  String: sMsgParametro
.head 4 +  Actions
.head 5 -  Call SybGetNextServerMsg( hSql, nErro, sMsgErro, nSeveridade )
.head 5 +  If sMsgErro != ''
.head 6 +  If bMostraNumero
.head 7 +  If lParam = 22100
.head 8 -  Set nErro = nErro + 65536
.head 7 +  ! If SalStrScan ( sMsgErro, 'constraint name') != -1
.head 8 -  Set sMensagem			= sMsgErro
.head 8 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'dbname')
.head 8 -  Set nPosicaoBanco 		= nPosicaoBanco + 8
.head 8 -  Set nPosicaoTabela		= SalStrScan( sMensagem, 'table name')
.head 8 -  Set sMsgErro =  ' Caso você esteja desejando incluir ou excluir esse registro, a operação não pode ser realizada, na tabela  ' 
.head 8 -  ! Set sMsgErro =  ' Registro não pode ser incluído na tabela  ' 
.head 8 -  Set nPosicaoTabela 		= nPosicaoTabela + 12
.head 8 -  Set nPosicaoConstraint 	= SalStrScan ( sMensagem, 'constraint name')
.head 8 -  Set sMsgErro =  sMsgErro || SalStrMidX(sMensagem,nPosicaoTabela+2,nPosicaoConstraint - nPosicaoTabela-5 )
.head 8 -  Set sMsgErro = sMsgErro || ', pois não existe relacionamento com a tabela pai ( ' ||  SalStrMidX(sMensagem,nPosicaoConstraint+19,50) 
.head 8 -  Set sMsgErro = SalStrMidX( sMsgErro, 1, SalStrLength( sMsgErro )  - 4) || ')'
.head 7 +  ! Else If SalStrScan ( sMsgErro, 'duplicate') != -1
.head 8 -  Set sMensagem			= sMsgErro
.head 8 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'Object')
.head 8 -  Set nPosicaoTabela 		= SalStrScan ( sMensagem, 'with')
.head 8 -  Set sMsgErro =  ' Registro não pode ser incluído, pois houve uma tentativa de inclusão de registro em duplicidade na tabela' || SalStrMidX(sMensagem,(nPosicaoBanco+8),nPosicaoTabela - nPosicaoBanco-10)
.head 8 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'index')
.head 8 -  Set sMsgErro =  sMsgErro || ' ( ' ||SalStrMidX(sMensagem,(nPosicaoBanco+7),50)
.head 8 -  Set sMsgErro = SalStrMidX( sMsgErro, 1, SalStrLength( sMsgErro )  - 3) || ')'
.head 7 +  Select Case nErro
.head 8 +  Case 546
.head 9 -  Set sMensagem			= sMsgErro
.head 9 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'dbname')
.head 9 -  Set nPosicaoBanco 		= nPosicaoBanco + 8
.head 9 -  Set nPosicaoTabela		= SalStrScan( sMensagem, 'table name')
.head 9 -  Set sMsgErro =  ' Registro não pode ser incluído na tabela  ' 
.head 9 -  Set nPosicaoTabela 		= nPosicaoTabela + 12
.head 9 -  Set nPosicaoConstraint 	= SalStrScan ( sMensagem, 'constraint name')
.head 9 -  Set sMsgErro =  sMsgErro || SalStrMidX(sMensagem,nPosicaoTabela+2,nPosicaoConstraint - nPosicaoTabela-5 )
.head 9 -  Set sMsgErro = sMsgErro || ', pois não existe relacionamento com a tabela pai ( ' ||  SalStrMidX(sMensagem,nPosicaoConstraint+19,50) 
.head 9 -  Set sMsgErro = SalStrMidX( sMsgErro, 1, SalStrLength( sMsgErro )  - 4) || ')'
.head 9 -  Break
.head 8 +  Case 547
.head 9 -  Set sMensagem			= sMsgErro
.head 9 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'dbname')
.head 9 -  Set nPosicaoBanco 		= nPosicaoBanco + 8
.head 9 -  Set nPosicaoTabela		= SalStrScan( sMensagem, 'table name')
.head 9 -  Set sMsgErro =  ' Registro não pode ser excluído, pois o mesmo está relacionado com outras tabelas do banco ' || SalStrMidX(sMensagem,(nPosicaoBanco+3),nPosicaoTabela - nPosicaoBanco-6)
.head 9 -  Set nPosicaoTabela 		= nPosicaoTabela + 12
.head 9 -  Set nPosicaoConstraint 	= SalStrScan ( sMensagem, 'constraint name')
.head 9 -  ! Set sMsgErro =  sMsgErro || ', tabela ' ||  SalStrMidX(sMensagem,nPosicaoTabela+2,nPosicaoConstraint - nPosicaoTabela-5 )
.head 9 -  Set sMsgErro = sMsgErro || ' (' ||  SalStrMidX(sMensagem,nPosicaoConstraint+19,50) || ' ) '
.head 9 -  Set sMsgErro = SalStrMidX( sMsgErro, 1, SalStrLength( sMsgErro ) - 4) || ')'
.head 9 -  Break
.head 8 +  Case 2601
.head 9 -  Set sMensagem			= sMsgErro
.head 9 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'Object')
.head 9 -  Set nPosicaoTabela 		= SalStrScan ( sMensagem, 'with')
.head 9 -  Set sMsgErro =  ' Registro não pode ser incluído, pois houve uma tentativa de inclusão de registro em duplicidade na tabela' || SalStrMidX(sMensagem,(nPosicaoBanco+8),nPosicaoTabela - nPosicaoBanco-10)
.head 9 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'index')
.head 9 -  Set sMsgErro =  sMsgErro || ' ( ' ||SalStrMidX(sMensagem,(nPosicaoBanco+7),50)
.head 9 -  Set sMsgErro = SalStrMidX( sMsgErro, 1, SalStrLength( sMsgErro )  - 3) || ')'
.head 9 -  Break
.head 8 +  Case 68137
.head 9 -  Set sMensagem			= sMsgErro
.head 9 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'Object')
.head 9 -  Set nPosicaoTabela 		= SalStrScan ( sMensagem, 'with')
.head 9 -  Set sMsgErro =  ' Registro não pode ser incluído, pois houve uma tentativa de inclusão de registro em duplicidade na tabela' || SalStrMidX(sMensagem,(nPosicaoBanco+8),nPosicaoTabela - nPosicaoBanco-10)
.head 9 -  Set nPosicaoBanco 		= SalStrScan ( sMensagem, 'index')
.head 9 -  Set sMsgErro =  sMsgErro || ' ( ' ||SalStrMidX(sMensagem,(nPosicaoBanco+7),50)
.head 9 -  Set sMsgErro = SalStrMidX( sMsgErro, 1, SalStrLength( sMsgErro )  - 3) || ')'
.head 9 -  Break
.head 6 +  If SalStrIsValidNumber( SalStrMidX( sMsgErro,0, 7) )
.head 7 -  Set nCodigoMSG = SalStrToNumber(SalStrMidX( sMsgErro,3, 4))
.head 7 -  Set nCodigoSistema = SalStrToNumber(SalStrMidX( sMsgErro,0, 3))
.head 7 -  Set sMsgErro = SalStrMidX( sMsgErro,8, 200)
.head 7 -  Set sMsgParametro = sMsgErro
.head 7 -  Set sMsgErro = SalNumberToStrX( nErro,0) || ' - ' || SalStrTrimX(  sMsgErro ) || '.  Deseja visualizar a ajuda da mensagem?'
.head 7 -  Call SalMessageBeep( 0)
.head 7 +  If SalMessageBox( sMsgErro, 'Erro', MB_YesNo) = IDYES
.head 8 -  Call SalModalDialog( dlgHelpMensagem, hWndNULL, nCodigoMSG,nCodigoSistema, sMsgParametro)
.head 6 +  Else
.head 7 -  Call SalMessageBeep( 0)
.head 7 -  Call SalMessageBox( sMsgErro, 'Erro', MB_Ok | MB_IconExclamation)
.head 5 +  Else
.head 6 -  Call SqlGetError( hSql, nErro, sMsgErro )
.head 6 +  Select Case nErro
.head 7 +  Case 401
.head 8 -  Set sMsgErro =  'Erro na configuração do alias no arquivo SQL.INI'
.head 8 -  Break
.head 7 +  Case 20911
.head 8 -  Set sMsgErro = 'Database nao encontrado no Servidor de Dados.'
.head 8 -  Break
.head 7 +  Case 30003
.head 8 -  Set sMsgErro = 'Servidor de Dados não localizado.'
.head 8 -  Break
.head 7 +  Case 30004
.head 8 -  Set sMsgErro = 'Servidor de Dados não disponível.'
.head 8 -  Break
.head 7 +  Case 30147
.head 8 -  Set sMsgErro = 'Usuário e/ou Senha Incorretos.'
.head 8 -  Break
.head 7 +  Default
.head 8 -  Set sMsgErro =  SqlGetErrorTextX ( nErro)
.head 8 -  Break
.head 6 +  If bMostraNumero
.head 7 -  Set sMsgErro = SalNumberToStrX( nErro,0) || ' - ' || sMsgErro
.head 6 -  Call SalMessageBeep( 0)
.head 6 -  Call SalMessageBox( sMsgErro, 'Erro', MB_Ok | MB_IconExclamation)
.head 5 -  ! Mostra a janela com a mensagem de erro
.head 5 -  Return FALSE
.head 3 +  Function: fMPSConnect
.head 4 -  Description: Ativa um Sql Handle - Criando FRS
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Receive Sql Handle: hSql
.head 4 -  Static Variables
.head 4 +  Local variables
.head 5 -  Boolean: bConectou
.head 4 +  Actions
.head 5 +  If SqlConnect( hSql)
.head 6 -  Call SqlSetResultSet( hSql, TRUE )
.head 6 -  Return TRUE
.head 5 +  Else
.head 6 -  Return FALSE
.head 3 +  Function: fMPSDisconnect
.head 4 -  Description: Desativa um Sql Handle
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Receive Sql Handle: hSql
.head 4 -  Static Variables
.head 4 -  Local variables
.head 4 +  Actions
.head 5 -  Return SqlDisconnect( hSql)
.head 3 +  Function: fMPSPrepareProc
.head 4 -  Description: Prepara a execução de uma Stored Procedure
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Sql Handle: hSqlx
.head 5 -  Long String: sComando
.head 5 -  Long String: sInto
.head 4 -  Static Variables
.head 4 +  Local variables
.head 5 -  Number: nProcuraParametro
.head 5 -  Number: nProcuraParametro2
.head 4 +  Actions
.head 5 -  Set hSqlErro = hSqlx
.head 5 -  ! --- 
Inserir a máquina(computador) que esta sendo utilizado ao chamar uma procedure. 
Só será necessário informar o parâmetro @Maquina no final de cada declaração de variaveis nas procedures.
! ---
.head 5 -  Set hSqlErro = hSqlx
.head 5 +  If sGlobalControleMaquina = 'S'
.head 6 +  If sGlobalMaquina != STRING_Null
.head 7 -  ! < 0 não encontrou || > -1 encontrou !
.head 7 -  Set nProcuraParametro2 =  SalStrScan( SalStrLowerX(sComando), 'select'  )
.head 7 +  If nProcuraParametro2 < 0
.head 8 -  Set nProcuraParametro =  SalStrScan( sComando, '@' )
.head 8 +  If nProcuraParametro  > -1
.head 9 -  Set sComando = sComando || ', @Maquina = :sGlobalMaquina'
.head 8 +  Else
.head 9 -  Set sComando = sComando || ' @Maquina = :sGlobalMaquina'
.head 5 -  Return SybPrepareProc( hSqlx, sComando, sInto )
.head 3 +  Function: fMPSPrepare
.head 4 -  Description: Prepara a execução de um comando SQL
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Sql Handle: hSqlx
.head 5 -  Long String: sComando
.head 4 -  Static Variables
.head 4 -  Local variables
.head 4 +  Actions
.head 5 -  Set hSqlErro = hSqlx
.head 5 -  Return SqlPrepare( hSqlx, sComando)
.head 3 +  Function: fMPSExecute
.head 4 -  Description: Executa uma Stored Procedure
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Receive Sql Handle: hSqlExec
.head 4 -  Static Variables
.head 4 -  Local variables
.head 4 +  Actions
.head 5 -  Return SqlExecute( hSqlExec)
.head 3 -  !
.head 3 +  Function: fMPSExecuteProcEx
.head 4 -  Description: Prepara a execução de uma Stored Procedure
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Sql Handle: hSqlx
.head 5 -  Long String: sComando
.head 5 -  Long String: sInto
.head 5 -  Receive Boolean: bRetorno
.head 4 -  Static Variables
.head 4 -  Local variables
.head 4 +  Actions
.head 5 -  Set hSqlErro = hSqlx
.head 5 -  Return SybExecuteProcEx( hSqlx, sComando, sInto, bRetorno )
.head 3 +  Function: fMPSGetNextResults
.head 4 -  Description: Pega próximo resultado
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Sql Handle: hSqlx
.head 5 -  Long String: sInto
.head 4 -  Static Variables
.head 4 -  Local variables
.head 4 +  Actions
.head 5 -  Set hSqlErro = hSqlx
.head 5 -  Return SybGetNextResults( hSqlx, sInto )
.head 3 +  Function: fReplaceString
.head 4 -  Description:
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Receive String: sSource
.head 5 -  String: sArgument
.head 5 -  String: sReplace
.head 4 -  Static Variables
.head 4 +  Local variables
.head 5 -  Number: nPos
.head 4 +  Actions
.head 5 +  While (nPos >= 0)
.head 6 -  Set nPos = SalStrScan(sSource,sArgument)
.head 6 +  If (nPos >= 0)
.head 7 +  If (sArgument = '\\%')
.head 8 -  Set sSource = SalStrReplaceX(sSource,nPos,1,sReplace)
.head 7 +  Else
.head 8 -  Set sSource = SalStrReplaceX(sSource,nPos,SalStrLength(sArgument),sReplace)
.head 2 -  Named Menus
.head 2 +  Class Definitions
.data RESOURCE 0 0 1 2706837895
0000: 5300000042000000 0000000000000000 0200000100FFFF01 00160000436C6173
0020: 73566172004F7574 6C696E6552006567 496E666F04003C00 000663004461646F
0040: 73220000E4000100 00290400000200FC 0000FF07
.enddata
.head 3 +  Functional Class: cDados
.head 4 -  Description: Classe que faz interface de conexão com o banco chamando as funções
do SYBASE
.head 4 -  Derived From
.head 4 -  Class Variables
.head 4 +  Instance Variables
.head 5 -  Sql Handle: ihSql
.head 5 -  String: isParametros
.head 5 -  Date/Time: idDate
.head 4 +  Functions
.head 5 +  Function: fGetHandle
.head 6 -  Description:
.head 6 +  Returns
.head 7 -  Sql Handle:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return ihSql
.head 5 +  Function: fSetHandle
.head 6 -  Description:
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  Sql Handle: hSql
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Set ihSql = hSql
.head 5 -  !
.head 5 +  Function: fCreate
.head 6 -  Description: Método para estabelecer a conexão com o banco
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  Sql Handle: hSql
.head 6 +  Actions
.head 7 -  ! Set hSql = fGetHandle()
.head 7 +  If (SqlConnect( ihSql ))
.head 8 -  Call SqlSetParameter( ihSql, DBP_PRESERVE, TRUE, '' )
.head 8 -  ! Call fSetHandle(hSql)
.head 8 -  Return ..fCreateV(  )
.head 7 +  Else
.head 8 -  Return FALSE
.head 5 +  Function: fDestroy
.head 6 -  Description: Método para estabelecer a conexão com o banco
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  Boolean: bReturn
.head 6 +  Actions
.head 7 -  Set bReturn = SqlCommit( ihSql ) and ..fDestroyV() and SqlDisconnect( ihSql )
.head 7 +  If bReturn
.head 8 -  Set ihSql = hWndNULL
.head 7 -  Return (bReturn)
.head 5 -  !
.head 5 +  Function: fPrepare
.head 6 -  Description: Método para preparar um comando sql
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 +  Parameters
.head 7 -  String: sCmdLine
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If SqlPrepare( fGetHandle(), sCmdLine )
.head 8 -  Return TRUE
.head 7 +  Else
.head 8 -  Return FALSE
.head 5 +  Function: fPrepareProc
.head 6 -  Description: Método para preparar um comando sql
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 +  Parameters
.head 7 -  String: sCmdLine
.head 7 -  String: sRetorno
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If SybPrepareProc( fGetHandle(), sCmdLine, sRetorno )
.head 8 -  Return TRUE
.head 7 +  Else
.head 8 -  Return FALSE
.head 5 +  Function: fPrepareAndExecute
.head 6 -  Description: Método para executar um comando sql
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 +  Parameters
.head 7 -  String: sCmdLine
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If SqlPrepare( fGetHandle(), sCmdLine )
.head 8 +  If (..fExecute( fGetHandle() ))
.head 9 -  Return TRUE
.head 7 +  Else
.head 8 -  Return FALSE
.head 5 +  Function: fPrepareAndExecuteProc
.head 6 -  Description: faz o mesmo que fPrepareAndExecute só que para precedures
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: sSql
.head 7 -  String: sInto
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  Boolean: bReturn
.head 6 +  Actions
.head 7 -  Set bReturn = SybPrepareProc( fGetHandle(), sSql, sInto )
.head 7 +  If (bReturn)
.head 8 +  If (..fExecute( fGetHandle() ))
.head 9 -  Return TRUE
.head 8 +  Else
.head 9 -  Set bReturn = FALSE
.head 7 -  Return (bReturn)
.head 5 -  !
.head 5 +  Function: fExecute
.head 6 -  Description:
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 +  Parameters
.head 7 -  Sql Handle: hSql
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return SqlExecute( ihSql )
.head 5 +  Function: fRead
.head 6 -  Description: Chama a procedure de leitura do banco de cada classe especifica.
Se a pesquisa for pela chave já é dado o fetch
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 +  Parameters
.head 7 -  Number: nTipoPesquisa
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  Boolean: bReturn
.head 6 +  Actions
.head 7 -  Set bReturn = ..fReadV(nTipoPesquisa)
.head 7 -  Return bReturn
.head 5 +  Function: fGetServerDate
.head 6 -  Description: Retorna a data do servidor
.head 6 +  Returns
.head 7 -  Date/Time:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  Date/Time: dData
.head 6 +  Actions
.head 7 -  Call fPrepareAndExecuteProc('exec dbhcen..GetServerDate -- Pega a Data/Hora Atual',  ':dData' )
.head 7 +  If SqlFetchNext( ihSql, lParam )
.head 8 -  Return dData
.head 7 +  Else
.head 8 -  Return DATETIME_Null
.head 5 -  !
.head 5 +  Function: fSetResultSet
.head 6 -  Description:
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  Boolean: pbStatus
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Call SqlSetResultSet( fGetHandle(), pbStatus)
.head 5 +  Function: fOpen
.head 6 -  Description:
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psCursor
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Call SqlOpen(fGetHandle(), psCursor)
.head 5 +  Function: fSybWriteText
.head 6 -  Description:
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psCursor
.head 7 -  Number: pnColumn
.head 7 -  String: psColumnName
.head 7 -  Long String: psUpdate
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Call SybWriteText( fGetHandle(), psCursor, pnColumn, psColumnName, psUpdate, 0, 0)
.head 5 -  !
.head 5 -  ! Parametros
.head 5 +  Function: fAddParamString
.head 6 -  Description: Adciona ao parametro da procedure uma string
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psParametro
.head 7 -  String: psValue
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If isParametros = ''
.head 8 -  Set isParametros = psParametro || ' = ' || '"'|| SalStrTrimX( psValue ) ||'"'
.head 7 +  Else
.head 8 -  Set isParametros = isParametros || ', ' || psParametro || ' = ' ||  ' "'|| SalStrTrimX( psValue ) ||'"'
.head 5 +  Function: fAddParamNumber
.head 6 -  Description: Adciona ao parametro da procedure um Number
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psParametro
.head 7 -  Number: pnValue
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If pnValue != NUMBER_Null
.head 8 +  If isParametros = ''
.head 9 -  Set isParametros =  psParametro || ' = ' || SalNumberToStrX( pnValue, 0 )
.head 8 +  Else
.head 9 -  Set isParametros = isParametros||', '||  psParametro || ' = ' || SalNumberToStrX(pnValue , 0)
.head 7 +  Else
.head 8 -  Call fAddParamNull(psParametro)
.head 5 +  Function: fAddParamFloat
.head 6 -  Description: Adciona ao parametro da procedure um Number
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psParametro
.head 7 -  Number: pnValue
.head 7 -  Number: pnDecimais
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  String: sNumero
.head 6 +  Actions
.head 7 +  If pnValue != NUMBER_Null
.head 8 -  Set sNumero = SalNumberToStrX( pnValue, pnDecimais )
.head 8 -  Call fReplaceString( sNumero, ',', '.' )
.head 8 -  Set sNumero = psParametro || ' = ' || sNumero
.head 8 +  If isParametros = ''
.head 9 -  Set isParametros = sNumero
.head 8 +  Else
.head 9 -  Set isParametros = isParametros||', '|| sNumero
.head 7 +  Else
.head 8 -  Call fAddParamNull(psParametro)
.head 5 +  Function: fAddParamDate
.head 6 -  Description: Adciona ao parametro da procedure uma Data
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psParametro
.head 7 -  Date/Time: pdValue
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If pdValue != DATETIME_Null
.head 8 +  If isParametros = ''
.head 9 -  Set isParametros = '"'|| psParametro || ' = ' || SalFmtFormatDateTime( pdValue, 'MM/dd/yyyy' ) ||'"'
.head 8 +  Else
.head 9 -  Set isParametros = isParametros||', "'|| psParametro || ' = ' || SalFmtFormatDateTime(pdValue,'MM/dd/yyyy') ||'"'
.head 7 +  Else
.head 8 -  Call fAddParamNull(psParametro)
.head 5 +  Function: fAddParamTime
.head 6 -  Description: Adciona ao parametro da procedure uma Data
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psParametro
.head 7 -  Date/Time: pdValue
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If pdValue != DATETIME_Null
.head 8 +  If isParametros = ''
.head 9 -  Set isParametros = '"'||  psParametro || ' = ' ||  SalFmtFormatDateTime( pdValue, 'hhhh:mm' ) ||'"'
.head 8 +  Else
.head 9 -  Set isParametros = isParametros||', "'||  psParametro || ' = ' ||  SalFmtFormatDateTime(pdValue,'hhhh:mm') ||'"'
.head 7 +  Else
.head 8 -  Call fAddParamNull(psParametro)
.head 5 +  Function: fAddParamDateTime
.head 6 -  Description: Adciona ao parametro da procedure uma Data
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psParametro
.head 7 -  Date/Time: pdValue
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If pdValue != DATETIME_Null
.head 8 +  If isParametros = ''
.head 9 -  Set isParametros = '"'||  psParametro || ' = ' ||  SalFmtFormatDateTime( pdValue, 'MM/dd/yyyy hhhh:mm' ) ||'"'
.head 8 +  Else
.head 9 -  Set isParametros = isParametros||', "'||  psParametro || ' = ' ||  SalFmtFormatDateTime(pdValue,'MM/dd/yyyy hhhh:mm') ||'"'
.head 7 +  Else
.head 8 -  Call fAddParamNull(psParametro)
.head 5 +  Function: fAddParamNull
.head 6 -  Description: Adciona ao parametro da procedure null
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: psParametro
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If isParametros = ''
.head 8 -  Set isParametros = psParametro || ' = null '
.head 7 +  Else
.head 8 -  Set isParametros = isParametros|| ', ' || psParametro || ' = null'
.head 5 -  !
.head 5 +  Function: fFormatParam
.head 6 -  Description: Retorna o parametro formatado para a execução da procedure
.head 6 +  Returns
.head 7 -  String:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return isParametros
.head 5 +  Function: fClearParam
.head 6 -  Description: Retorna o parametro formatado para a execução da procedure
.head 6 -  Returns
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Set isParametros = ''
.head 5 +  Function: fFormatInto
.head 6 -  Description: Formata uma string com o nome de todas as variáveis de instância
EX:

	':inCgcCfc, :isNome,......'
.head 6 +  Returns
.head 7 -  String:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 -  Actions
.head 5 +  Function: fPreparaParametro
.head 6 -  Description:
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: sTipoOperacao
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Call ..fPreparaParametro( sTipoOperacao )
.head 5 +  Function: fGetNomePesquisa
.head 6 -  Description:
.head 6 +  Returns
.head 7 -  String:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return ..fGetNomePesquisa()
.head 5 +  Function: fGetCodigoPesquisa
.head 6 -  Description:
.head 6 +  Returns
.head 7 -  String:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return ..fGetCodigoPesquisa()
.head 5 +  Function: fSetNomePesquisa
.head 6 -  Description:
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  String: ps
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Call ..fSetNomePesquisa( ps )
.head 5 +  Function: fGetDate
.head 6 -  Description: Retorna a data que está carregadaa
na variável de instância
.head 6 +  Returns
.head 7 -  Date/Time:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return idDate
.head 5 -  !
.head 5 -  !
.head 5 +  Function: fPopulaTabela
.head 6 -  Description: Popula a tabela
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  Window Handle: hWndRelatorio
.head 7 -  String: sProcedure
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  Sql Handle: hSqlPoupula
.head 7 -  Boolean: bRetorno
.head 6 +  Actions
.head 7 -  !
.head 7 +  If fCreate(  )
.head 8 -  Call SqlSetInMessage(  fGetHandle ( ), 32000 )
.head 8 +  If fPrepareProc(sProcedure, fMontaRetorno(hWndRelatorio) )
.head 9 +  If SalTblPopulate( hWndRelatorio, fGetHandle ( ), STRING_Null , TBL_FillAll )
.head 10 -  Set bRetorno = TRUE
.head 8 -  Return bRetorno
.head 8 -  Call fDestroy(  )
.head 7 -  !
.head 5 +  Function: fMontaRetorno
.head 6 -  Description: Mosta as colunas para arquivo de retorno
.head 6 +  Returns
.head 7 -  String:
.head 6 +  Parameters
.head 7 -  Window Handle: hWndRelatorio
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  Window Handle: hWndChild
.head 7 -  String: sNome
.head 7 -  String: sNomeTabela
.head 7 -  String: sRetorno
.head 6 +  Actions
.head 7 -  Set sRetorno = STRING_Null
.head 7 -  ! Call SalGetItemName(hWndRelatorio, sNomeTabela)
.head 7 -  Set sNomeTabela = 'hWndRelatorio'
.head 7 -  Set hWndChild = SalGetFirstChild( hWndRelatorio, TYPE_TableColumn )
.head 7 -  Call SalGetItemName( hWndChild, sNome)
.head 7 -  Set sNome = ':' || sNomeTabela || '.' || sNome
.head 7 -  Set sRetorno = sRetorno || sNome
.head 7 -  !
.head 7 +  Loop
.head 8 -  Set hWndChild = SalGetNextChild( hWndChild, TYPE_TableColumn )
.head 8 +  If not  SalGetItemName(  hWndChild, sNome )
.head 9 -  Break
.head 8 -  Set sNome = ', :' || sNomeTabela || '.' || sNome
.head 8 -  Set sRetorno = sRetorno || sNome
.head 7 -  ! Call
.head 7 -  Return sRetorno
.head 5 -  !
.head 5 -  ! Virtuais
.head 5 +  Function: fFetch
.head 6 -  Description: Faz o fetch para esta classe quando queremos carregar
as variáveis da classe
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return SqlFetchNext( fGetHandle(), lParam )
.head 5 +  Function: fReadV
.head 6 -  Description: Chama a procedure de leitura do banco de cada classe especifica
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 +  Parameters
.head 7 -  Number: nTipoPesquisa
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Call SalMessageBox( 'Método fReadV não foi encontrado!', 'Atenção', MB_IconExclamation )
.head 7 -  Return FALSE
.head 5 +  Function: fCreateV
.head 6 -  Description: Método imlpementado na subclasse
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return TRUE
.head 5 +  Function: fDestroyV
.head 6 -  Description: Método imlpementado na subclasse
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Return TRUE
.head 5 +  Function: fInsertChangeDeleteGeneric
.head 6 -  Description:
.head 6 +  Returns
.head 7 -  Boolean:
.head 6 +  Parameters
.head 7 -  String: sOperacao
.head 7 -  String: sStoredProc
.head 6 +  Static Variables
.head 7 -  String: sCmdLine
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Call fClearParam()
.head 7 -  Call ..fPreparaParametro(sOperacao)
.head 7 -  Set sCmdLine = 'exec ' || sStoredProc || fFormatParam()
.head 7 -  Return fPrepareAndExecute(sCmdLine)
.head 2 +  Default Classes
.head 3 -  MDI Window: cQCKMDI
.head 3 -  Form Window: cQCK1Form
.head 3 -  Dialog Box:
.head 3 -  Table Window:
.head 3 -  Quest Window:
.head 3 -  Data Field:
.head 3 -  Spin Field:
.head 3 -  Multiline Field:
.head 3 -  Pushbutton:
.head 3 -  Radio Button:
.head 3 -  Option Button:
.head 3 -  ActiveX:
.head 3 -  Check Box:
.head 3 -  Child Table:
.head 3 -  Quest Child Window:
.head 3 -  List Box: bListBox
.head 3 -  Combo Box: cQCK1ComboBox
.head 3 -  Picture:
.head 3 -  Vertical Scroll Bar:
.head 3 -  Horizontal Scroll Bar:
.head 3 -  Column: cQCK1Column
.head 3 -  Background Text: cQCK1BackgroundText
.head 3 -  Group Box:
.head 3 -  Line:
.head 3 -  Frame:
.head 3 -  Custom Control:
.head 2 +  Application Actions
.head 3 +  On SAM_SqlError
.head 4 -  Return fTratamentoErroSql( wParam, lParam, hSqlErro, TRUE )
